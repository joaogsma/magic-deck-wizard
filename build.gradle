import org.ajoberstar.grgit.Grgit

plugins {
  id 'idea'
  id 'scala'
  id 'java'
  id 'application'
  id 'org.ajoberstar.grgit' version '1.7.2'
  id 'org.ajoberstar.release-opinion' version '1.7.2'
  id 'com.github.johnrengelman.shadow' version '4.0.2'
  id 'com.google.protobuf' version '0.8.8'
}

mainClassName = 'org.joaogsma.controllers.ConsoleController'

idea {
  module {
    sourceDirs += file("${projectDir}/build/generated/source/proto/main/java")
  }
}

ext {
  scalaVersion = '2.13.4'
  circleVersion = '0.13.0'
  declineVersion = '1.3.0'
  protoBufVersion = '3.7.0-rc1'
  scalatestVersion = '3.2.3'
  scalatestMockitoVersion = '3.2.2.0'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

release {
  grgit = Grgit.open(currentDir: file('.'))
}

static def removePatch(String str) {
  return str.substring(0, str.lastIndexOf('.'))
}

dependencies {
  compile "org.scala-lang:scala-library:$scalaVersion"
  compile "io.circe:circe-core_${removePatch(scalaVersion)}:$circleVersion"
  compile "io.circe:circe-generic_${removePatch(scalaVersion)}:$circleVersion"
  compile "io.circe:circe-parser_${removePatch(scalaVersion)}:$circleVersion"
  compile "com.monovore:decline_${removePatch(scalaVersion)}:$declineVersion"
  compile "com.google.protobuf:protobuf-java:$protoBufVersion"

  testCompile "org.scalatest:scalatest_${removePatch(scalaVersion)}:$scalatestVersion"
  testCompile "org.scalatestplus:mockito-3-4_${removePatch(scalaVersion)}:$scalatestMockitoVersion"
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.0.0'
  }
}

shadowJar {
  minimize()
  version = project.version
}

task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
  main = 'org.scalatest.tools.Runner'
  args = ['-R', 'build/classes/scala/test', '-o']
  classpath = sourceSets.test.runtimeClasspath
}

tasks.release.dependsOn build
tasks.test.dependsOn scalaTest
tasks.idea.dependsOn("generateProto")
